Alphabet
א ב ג ד ה ו ז ח ט י כ ל מ נ ס ע פ צ ק ר שׂ שׁ ת
ַ ָ ֵ ֶ ִ ֹ ֻ ְ ֲ ֱ ֳ
ּ

%>:0

! the lazy vowel
%{V%}:ַ %{V%}:ָ %{V%}:ֵ %{V%}:ֶ %{V%}:ִ %{V%}:ֹ %{V%}:ֻ
%{V%}:ְ %{V%}:ֲ %{V%}:ֱ %{V%}:ֳ

%{ps%}:0 %{ps%}:ְ %{ps%}:ֶ %{ps%}:ָ %{ps%}:ֵ !%{ps%}:i

%{and%}:%. %{and%}:ְ %{and%}:ַ
%{andc%}:ַ %{andc%}:ָ

%{%~á%}:ָ %{%~á%}:ְ
%{%~e%}:ֵ %{%~e%}:ְ

%{%~iy%}:ִ %{%~iy%}:0 %{%~iy%}:ֶ

%{short%}:0
%{consec%}:0
%{cons%}:0
%{pause%}:0

%{%+%*%}:%{%*%}
%{%-%*%}:0

0:ְ
0:ִ
 ;

Sets

Begedkefet = ב ג ד כ פ ת ;
Vowel = ַ ָ ֵ ֶ ִ ֹ ֻ ְ ֲ ֱ ֳ ;
Cons = א ב ג ד ה ו ז ח ט י כ ל מ נ ס ע פ צ ק ר שׂ שׁ ת ;

NonVowCons = ב ג ד ה ז ח ט כ ל מ נ ס ע פ צ ק ר שׂ שׁ ת ;

Stress = %{%+%*%} %{%-%*%} ;

Rules

"null @ or @ after final k"
0:ְ <=> :NonVowCons ( ּ ) _ :NonVowCons ;
         :NonVowCons ( ּ ) _ א ;
         ה _ י [ :Vowel | ו: ] ;
         כ ( :0 | :ּ )* _ .#. ;

"final y"
0:ִ <=> :Cons _ י :0* .#. ;

!!!!!!!!!!!!!!!
!!! h
!!!!!!!!!!!!!!!

"lamed-he verbs"
ה:0 <=> _ %>: %{short%}: ;
        %{%-%*%}: _ ;

!!!!!!!!!!!!!!!
!!! a
!!!!!!!!!!!!!!!

"blah"
ַ:ָ <=> _ %{%-%*%}: א ;

"ay → ey cons"
ַ:ֵ <=> _ י ?* %{cons%}: ;

"stress shift a"
ַ:ֶ <=> _ %{%-%*%}: ;
        except _ %{%-%*%}: [ א | ה: | ח ] ;

"shress shift é h"
ֶ:0 <=> _ %{%-%*%}: ה: ;

"stress shift e"
ֵ:ֶ <=> _ %{%-%*%}: ;
        except !. _ ; ! TODO: is this right?
          _ :* %{pause%}: ;

"stress shift to ä"
ֲ:ַ <=> _ %{%+%*%}: ;
        except _ %{%+%*%}: י :0* .#. ;

"stress shift to äy#"
ֲ:ִ <=> _ %{%+%*%}: י :0* .#. ;

"stress shift hollow verbs 1"
ו:ָ <=> _ ּ:0 %{%-%*%}: ;
        except _ ּ: %{%-%*%}: .#. ;

"stress shift hollow verbs 2"
ּ:0 <=> ו:ָ _ %{%-%*%}: ;
        %{andc%}: י _ :ְ ;

!!!!!!!!!!!!!!!
!!! OTHER
!!!!!!!!!!!!!!!

"3sg poss after y"
ֹ:0 <=> י %{ps%}: ו: _ ;

"weaken final vowel in construct when suffix"
V1:V2 <=> _ [ Cons - י - ח ] %{cons%}: ;
          !except .#. Cons ( . ) _ Cons %{cons%}: .#. ;
          except _ Cons %{cons%}: .#. ;
      where V1 in ( ַ  ָ  ֵ  ֶ  )
            V2 in ( ְ ְ ְ ְ )
      matched ;

"weaken only vowel in construct"
V1:V2 <=> .#. Cons ( ּ ) _ [ Cons - י - ח ] %{cons%}: .#. ;
      where V1 in ( ֵ )
            V2 in ( ֶ )
      matched ;

"weaken final vowel in construct before guttural?"
ָ:ֶ => _ ח %{cons%}: ;

"stress shift to i"
ִ:ַ <=> _ %{%+%*%}: ר ; ! ??? r'h but not qr' - now kind of final?

"double schwa"
ְ:ִ <=> _ Cons :ְ ;

"vav consec before aleph"
%{andc%}:ָ <=> _ א ;
               except _ א ֲ ;

"{and} only a sometimes"
%{and%}:ַ <=> _ [ א | %{%'%} | ח ] :ֲ ;

"stress shift from hiriq male' - i"
ִ:ֶ <=> _ י: %{%-%*%}: ;
        except _ י: %{%-%*%}: א ;

"stress shift from hiriq male' - iy'"
ִ:ֵ <=> _ י: %{%-%*%}: א ;

"stress shift from hiriq male' - y"
י:0 <=> [ ִ:ֶ | ִ:ֵ ] _ %{%-%*%}: ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

