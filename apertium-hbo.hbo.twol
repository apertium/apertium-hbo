Alphabet
%' b g d h w z x v y k l m n s %{'%} p c q r %{s%} %{sh%} t
a á e é i o u %@ ä ë ö
.


%@:0 %@:o

%>:0

! the lazy vowel
%{V%}

%{ps%}

%{and%}

%{%~a%}
%{%~á%}
%{%~e%}
%{i%}:i %{i%}:o %{i%}:a %{i%}:á %{i%}:%@ %{i%}:e
%{%~o%}:a %{%~o%}:%@ %{%~o%}:. %{%~o%}:é

%{%~w%}:w %{%~w%}:o

%{%~iy%}

%{h%}:h %{h%}:0
%{%~h%}:h %{%~h%}:0

%{short%}
%{consec%}
%{cons%}

%{%*%} %{%*%?%} %{%<%*%} %{%*%>%}

%{paal%}:0 %{nif%}:0 %{piel%}:0 %{pual%}:0 %{hif%}:0 %{hof%}:0 %{hit%}:0

 ;

Sets

Begedkefet = b g d k p t ;
Vowel = a á e é i o u %@ ä ë ö ;
Cons = %' b g d h %{%~h%} w z x v y k l m n s %{'%} p c q r %{s%} %{sh%} t ;

DageshAble = z y m q c %{s%} %{sh%} t ;

Binyan = %{paal%} %{nif%} %{piel%} %{pual%} %{hif%} %{hof%} %{hit%} ;

Stress = %{%*%} %{%*%?%} %{%<%*%} %{%*%>%} ;

Definitions

ImpfPref = %{i%}: ( %{%*%}: ) %>: ;
ImpfBd = %>: Binyan: ;
HollowVerb = Cons [ w: | %{%~w%}: ] ;
PehGuttural = [ %' | h | %{%'%} | x ] ;

Rules

!!!!!!!!!!!!!!!
!!! INSERTIONS
!!!!!!!!!!!!!!!

"insert dagesh"
0:. => .#. Begedkefet _ ;
       %{and%}: ( Stress: ) y _ %{i%}: ;
       %{h%}: a: %>: DageshAble: _ ;
       [ l:0 | y:0 | n:0 ] DageshAble: _ ;

"insert @ after final k"
0:%@ <=> k :0* _ .#. ;

"insert yod"
0:y => %{%~iy%}:i _ ;

"insert o"
0:o => y:w _ ;

!!!!!!!!!!!!!!!
!!! a
!!!!!!!!!!!!!!!

"determiner before gutturals"
a:á <=> %{h%}: _ %>: [ %': | %{%'%}: ] ;

!!!!!!!!!!!!!!!
!!! {and}
!!!!!!!!!!!!!!!

"{and} before b"
%{and%}:%. <=> _ b: ;

"{and} before y"
%{and%}:a <=> _ ( %{%<%*%}: ) y ;

!!!!!!!!!!!!!!!
!!! @
!!!!!!!!!!!!!!!

"schwa deletes"
%@:0 <=> _ ( %>: ) %{h%}: ;
         %' _ ;
         [ l:0 | y:0 | n:0 ] _ Cons ( 0:. ) %{%~o%}: ;
         ImpfPref Cons _ w: ;

!!!!!!!!!!!!!!!
!!! {h}
!!!!!!!!!!!!!!!

"{h} → 0 after vowel"
%{h%}:0 <=> Vowel: ( %>: ) _ ;

!!!!!!!!!!!!!!!
!!! i
!!!!!!!!!!!!!!!

"possessed plural construct 3sg"
i:á <=> %{cons%}: _ y: %{ps%}: w: ;

!!!!!!!!!!!!!!!
!!! {i}
!!!!!!!!!!!!!!!

"paal/nifal {i}"
%{i%}:i <=> _ %>: [ %{paal%}: | %{nif%}: ] ;
            except _ ImpfBd [ PehGuttural | HollowVerb ] ;

"{i} before guttural"
%{i%}:a <=> _ ImpfBd %{%'%} ;

"i → á in impf hollow verbs"
%{i%}:á <=> _ ImpfBd HollowVerb ;
            except _ [ ImpfBd PehGuttural | %>: %{piel%}: | %>: %{hif%}: ] ;

"impf → @"
%{i%}:%@ <=> _ %>: %{piel%}: ;
             _ %>: %{paal%}: [ h | x ] ;
             except _ %>: Binyan: %{%'%} ;

"drop impf i"
%{i%}:0 <=> _ %>: %{hif%}: y: ;

!!!!!!!!!!!!!!!
!!! l, n
!!!!!!!!!!!!!!!

"impf delete initial cons"
Cx:0 <=> %{i%}: %>: [ %{paal%}: | %{hif%}: ] _ ;
     where Cx in ( %{%~h%} l n ) ;

!!!!!!!!!!!!!!!
!!! {~o}
!!!!!!!!!!!!!!!

"{~o} deletes if suffix"
%{%~o%}:%@ <=> _ :* :Vowel ;
               _ :* w :. ;
               except [ w: | %{%~w%}: ] _ ;

"hollow verbs wo"
%{%~o%}:. <=> w: _ ;

"hollow verbs o"
%{%~o%}:0 <=> %{%~w%}:o _ ;

"lamed-heh paal"
%{%~o%}:é <=> _ ( Stress ) h %>: .#. ;
              except [ w: | %{%~w%}: ] _ ;

!!!!!!!!!!!!!!!
!!! y
!!!!!!!!!!!!!!!

"impf delete initial y"
y:0 <=> %{i%}: %>: Binyan: _ ;
        except %{hif%}: _ ;

"y in impf hifil"
y:w <=> %{i%}: %>: %{hif%}: _ ;

!!!!!!!!!!!!!!!
!!! OTHER
!!!!!!!!!!!!!!!

"w-o hollow verbs"
%{%~w%}:o <=> _ %{%~o%}: ;

"definite segolate nouns"
é:á <=> %{h%}: a: %>: %': _ ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

