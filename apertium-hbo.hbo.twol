Alphabet
א ב ג ד ה ו ז ח ט י כ ל מ נ ס ע פ צ ק ר שׂ שׁ ת
ַ ָ ֵ ֶ ִ ֹ ֻ ְ ֲ ֱ ֳ
ּ

י:0 י:ֹ

0:ּ

ְ:0

%>:0

%{ps%}

%{and%}
%{andc%}

%{%~a%}:ַ %{%~a%}:ְ %{%~a%}:ָ
%{%~á%}:ָ %{%~á%}:ְ
%{%~e%}:ֵ %{%~e%}:ַ %{%~e%}:ְ
%{%~é%}:ֶ %{%~é%}:0
%{i%}:ִ %{i%}:ֹ %{i%}:ַ %{i%}:ָ %{i%}:ְ %{i%}:ֵ %{i%}:ו
%{%~o%}:ְ %{%~o%}:ּ %{%~o%}:ֶ %{%~o%}:ֹ %{%~o%}:ַ %{%~o%}:ָ %{%~o%}:ֵ

%{%~w%}:ו %{%~w%}:ֹ

%{%~iy%}:ִ %{%~iy%}:ַ %{%~iy%}:ֵ

%{h%}:ה %{h%}:0
%{%~h%}:ה %{%~h%}:0

%{.%}:0 %{.%}:ּ

%{short%}
%{consec%}
%{cons%}
%{pause%}

%{%*%} %{%*%?%} %{%<%*%} %{%*%>%}

%{paal%}:0 %{nif%}:0 %{piel%}:0 %{pual%}:0 %{hif%}:0 %{hof%}:0 %{hit%}:0

 ;

Sets

Begedkefet = ב ג ד כ פ ת ;
Vowel = ַ ָ ֵ ֶ ִ ֹ ֻ ְ ֲ ֱ ֳ ;
Cons = א ב ג ד ה %{%~h%} ו ז ח ט י כ ל מ נ ס ע פ צ ק ר שׂ שׁ ת ;

DageshAble = ז י מ ק צ שׂ שׁ ת ;

Binyan = %{paal%} %{nif%} %{piel%} %{pual%} %{hif%} %{hof%} %{hit%} ;

StressMark = %{%*%} %{%*%?%} %{%<%*%} %{%*%>%} ;

Definitions

ImpfPref = %{i%}: ( %{%*%}: ) %>: ;
ImpfBd = %>: Binyan: ;
HollowVerb = [ Cons [ ו: | %{%~w%}: ] | ב ֹ: ] ;
PehGuttural = [ א | ה | ע | ח ] ;

C2Hollow = [ ו: | %{%~w%}: | ֹ: ] ;
V2DelC1 = [ %{%~h%}:0 | נ:0 | י:0 ] Cons ( 0:ּ ) ;
VbNoSuf = %>: ( %{pause%} ) .#. ;

Stress = ( StressMark: ) ;

Rules

!!!!!!!!!!!!!!!
!!! INSERTIONS
!!!!!!!!!!!!!!!

"insert dagesh"
0:ּ => %{h%}: ַ: %>: DageshAble: _ ;
       [ ל:0 | י:0 | נ:0 ] ( %>: ) DageshAble: _ ;
       except _ ּ ;

"drop dagesh"
ּ:0 <=> ה:י %>: ת _ ;
        ר _ ;

"drop maybe dagesh"
%{.%}:0 <=> _ .#. ;

!!!!!!!!!!!!!!!
!!! a
!!!!!!!!!!!!!!!

"determiner before gutturals"
ַ:ָ <=> %{h%}: _ %>: [ א: | ע: ] ;
       %{h%}: ַ: %>: ע _ ;
       %{piel%}: Cons _ Cons ּ:0 ;

!!!!!!!!!!!!!!!
!!! á
!!!!!!!!!!!!!!!

"feminine plural drop -áh: á"
ָ:0 <=> _ ה: %>: ו ֹ ת ;

!!!!!!!!!!!!!!!
!!! {~a}
!!!!!!!!!!!!!!!

"{~a} → á in lamed-hey"
%{%~a%}:ָ <=> _ ( Stress ) ה ;

"{~a} → a in closed syllables"
%{%~a%}:ַ <=> _ ( Stress ) Cons VbNoSuf ;
              _ ( Stress ) Cons: %>: Cons ;
              except _ ( Stress ) [ ה | Cons %>: ו ּ ] ;

!!!!!!!!!!!!!!!
!!! {~á}
!!!!!!!!!!!!!!!

"{~á}h deletes in lamed-hey plural"
%{%~á%}:0 <=> _ ( Stress ) ה: %>: ו ;

"schwa in paal perf V1"
%{%~á%}:ְ <=> _ :+ %>: ת ּ ֶ %{%*%} [ מ | נ ] .#. ;
               _ ?* %>: ?* %>: ;

"{~á} → i in lamed-hey with suffix"
%{%~á%}:ִ <=> _ ( Stress ) ה:י ;

!!!!!!!!!!!!!!!
!!! {and}
!!!!!!!!!!!!!!!

"{and} before b"
%{and%}:%. <=> _ ב: ;

!!!!!!!!!!!!!!!
!!! @
!!!!!!!!!!!!!!!

"schwa deletes"
ְ:0 <=> _ ( %>: ) %{h%}: ;
         א _ ;
         [ ל:0 | י:0 | נ:0 ] _ Cons ( 0:ּ ) %{%~o%}: ;
         ImpfPref Cons _ ו: ;

!!!!!!!!!!!!!!!
!!! e
!!!!!!!!!!!!!!!

"possessed plural construct 3sg"
ֵ:ָ <=> %{cons%}: _ י: %{ps%}: ו: ;
        _ Cons %>: ִ י מ ;

!!!!!!!!!!!!!!!
!!! é
!!!!!!!!!!!!!!!

"1sg impf by binyan"
ֶ:Vx <=> א _ %>: Bx: ;
         where Vx in ( ֲ        ֲ        ַ       ָ       )
               Bx in ( %{piel%} %{pual%} %{hif%} %{hof%} )
         matched ;

!!!!!!!!!!!!!!!
!!! {~e}
!!!!!!!!!!!!!!!

"{~e} deletes in open syllables"
%{%~e%}:ְ <=> _ ( Stress ) Cons %>: [ :Vowel | ו ּ ] ;

"{~e} → e in closed syllables final syllables"
%{%~e%}:ֵ <=> _ ( Stress ) Cons VbNoSuf ;
              [ %{piel%}: | %{hit%}: ] ?* _ ( Stress ) Cons: %>: נ ָ ;
              except _ ( Stress ) [ ה | Cons %>: ו ּ ] ;

!!!!!!!!!!!!!!!
!!! {~é}
!!!!!!!!!!!!!!!

"{~é}h deletes before vowels"
%{%~é%}:0 <=> _ ( Stress ) ה: ( %>: ) Vowel ;
              _ ( Stress ) ה: ( %>: ) ו ּ ;

!!!!!!!!!!!!!!!
!!! h
!!!!!!!!!!!!!!!

"feminine plural drop -áh: h"
ה:0 <=> ָ: _ %>: ו ֹ ת ;
        [ %{%~á%}:0 | %{%~é%}:0 ] ( Stress ) _ ;

"lamed-hey with suffix"
ה:י <=> _ %>: [ Cons - ו: ] ;

!!!!!!!!!!!!!!!
!!! {h}
!!!!!!!!!!!!!!!

"{h} → 0 after vowel"
%{h%}:0 <=> Vowel: ( %>: ) _ ;

!!!!!!!!!!!!!!!
!!! {i}
!!!!!!!!!!!!!!!

"paal/nifal {i}"
%{i%}:ִ <=> _ %>: [ %{paal%}: | %{nif%}: | %{hit%}: ] ;
            except _ ImpfBd [ PehGuttural | HollowVerb | י:0 | %{%~h%}:0 ] ;

"{i} before guttural or in hiph'il"
%{i%}:ַ <=> _ ImpfBd ע ;
            _ %>: %{hif%}: ;
            except
              _ %>: %{hif%}: [ י: | ו: ] ;
              _ %>: %{hif%}: Cons [ ו: | ֹ: | %{%~iy%}: ] ;

"{i} before hiph'il peh-yod"
%{i%}:0 <=> _ ImpfBd ו ;

"i → á in impf hollow verbs"
%{i%}:ָ <=> _ ImpfBd HollowVerb ;
            _ %>: %{hof%}: ;
            _ %>: %{hif%}: Cons %{%~iy%}: ;
            except
              _ [ ImpfBd PehGuttural | %>: %{piel%}: ] ;
              _ %>: %{hif%}: Cons Cons ;

"i → e in impf peh-yod"
%{i%}:ֵ <=> _ ImpfBd [ י:0 | %{%~h%}:0 ] ;

"impf → @"
%{i%}:ְ <=> _ %>: [ %{piel%}: | %{pual%}: ] ;
             _ %>: %{paal%}: [ ה | ח ] ;
             except _ %>: Binyan: ע ;

"drop impf i"
%{i%}:ו <=> _ %>: %{hif%}: י: ;

!!!!!!!!!!!!!!!
!!! {~iy}
!!!!!!!!!!!!!!!

"{~iy} → e when suffix is náh"
%{%~iy%}:ֵ <=> _ י: ( Stress ) Cons %>: נ ָ ;
               %{andc%}: %{%<%*%}: ?* %{hif%}: י: ?* _ ;

"{~iy} → a in most closed syllables"
%{%~iy%}:ַ <=> _ י: ( Stress ) Cons %>: [ Cons - ו ] ;
               except _ י: ( Stress ) Cons %>: נ ָ ;

!!!!!!!!!!!!!!!
!!! l, n
!!!!!!!!!!!!!!!

"impf delete initial cons"
Cx:0 <=> %{i%}: %>: [ %{paal%}: | %{hif%}: ] _ ;
     where Cx in ( %{%~h%} ל ) ;

"n deletes"
נ:0 <=> %{i%}: %>: [ %{paal%}: | %{hif%}: ] _ ;
        _ %>: ת ;

!!!!!!!!!!!!!!!
!!! {~o}
!!!!!!!!!!!!!!!

"{~o} deletes if suffix"
%{%~o%}:ְ <=> _ :* [ :Vowel - ָ ] ;
               _ :* ו :ּ ;
               except C2Hollow _ ;

"hollow verbs wo"
%{%~o%}:ּ <=> ו: _ ;

"hollow verbs o"
%{%~o%}:0 <=> %{%~w%}:ֹ _ ;

"{~o} in peh-aleph"
%{%~o%}:ַ <=> א: Cons _ ;
              _ ( Stress ) ח VbNoSuf ;
              except
                _ :* [ :Vowel - ָ ] ;
                _ :* ו :ּ ;
                C2Hollow _ ;

"{~o} in peh-yod"
%{%~o%}:ֵ <=> V2DelC1 ( ּ ) _ ( Stress ) Cons VbNoSuf ;
              except C2Hollow _ ;

"{~o} special lamed"
%{%~o%}:Vx <=> _ ( Stress ) Cx VbNoSuf ;
               except [ C2Hollow | V2DelC1 ] _ ;
               where Vx in ( ָ  ֶ )
                     Cx in ( א ה )
               matched ;

!!!!!!!!!!!!!!!
!!! y
!!!!!!!!!!!!!!!

"impf delete initial y"
י:0 <=> %{i%}: %>: [ Binyan: - %{hif%}: ] _ ;
        ַ י %>: ִ _ ;
        %{%~iy%}:ֵ _ ;
        %{%~iy%}:ַ _ ;

"y in impf hifil"
י:ֹ <=> %{i%}: %>: %{hif%}: _ ;

!!!!!!!!!!!!!!!
!!! OTHER
!!!!!!!!!!!!!!!

"w-o hollow verbs"
%{%~w%}:ֹ <=> _ %{%~o%}: ;

"definite segolate nouns"
ֶ:ָ <=> %{h%}: ַ: %>: א: _ ;

"delete stress if preceding vowel gets deleted"
%{%*%?%}:0 <=> %{%~é%}:0 _ ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst
