Alphabet
%' b g d h w z x v y k l m n s %{'%} p c q r %{s%} %{sh%} t
a á e é i o u %@ ä ë ö
.

y:0 y:o

0:.

%@:0

%>:0

%{ps%}

%{and%}

%{%~a%}:a %{%~a%}:%@ %{%~a%}:á
%{%~á%}:á %{%~á%}:%@
%{%~e%}:e %{%~e%}:a %{%~e%}:%@
%{i%}:i %{i%}:o %{i%}:a %{i%}:á %{i%}:%@ %{i%}:e %{i%}:w
%{%~o%}:%@ %{%~o%}:. %{%~o%}:é %{%~o%}:o %{%~o%}:a %{%~o%}:á %{%~o%}:e

%{%~w%}:w %{%~w%}:o

%{%~iy%}:i %{%~iy%}:a %{%~iy%}:e

%{h%}:h %{h%}:0
%{%~h%}:h %{%~h%}:0

%{short%}
%{consec%}
%{cons%}

%{%*%} %{%*%?%} %{%<%*%} %{%*%>%}

%{paal%}:0 %{nif%}:0 %{piel%}:0 %{pual%}:0 %{hif%}:0 %{hof%}:0 %{hit%}:0

 ;

Sets

Begedkefet = b g d k p t ;
Vowel = a á e é i o u %@ ä ë ö ;
Cons = %' b g d h %{%~h%} w z x v y k l m n s %{'%} p c q r %{s%} %{sh%} t ;

DageshAble = z y m q c %{s%} %{sh%} t ;

Binyan = %{paal%} %{nif%} %{piel%} %{pual%} %{hif%} %{hof%} %{hit%} ;

Stress = %{%*%} %{%*%?%} %{%<%*%} %{%*%>%} ;

Definitions

ImpfPref = %{i%}: ( %{%*%}: ) %>: ;
ImpfBd = %>: Binyan: ;
HollowVerb = Cons [ w: | %{%~w%}: ] ;
PehGuttural = [ %' | h | %{%'%} | x ] ;

C2Hollow = [ w: | %{%~w%}: ] ;
V2DelC1 = [ %{%~h%}:0 | n:0 | y:0 ] Cons ( 0:. ) ;
VbNoSuf = %>: .#. ;

Rules

!!!!!!!!!!!!!!!
!!! INSERTIONS
!!!!!!!!!!!!!!!

"insert dagesh"
0:. => !.#. Begedkefet _ ;
       %{and%}: ( Stress: ) y _ %{i%}: ;
       %{h%}: a: %>: DageshAble: _ ;
       [ l:0 | y:0 | n:0 ] ( %>: ) DageshAble: _ ;
       except _ . ;

!!!!!!!!!!!!!!!
!!! a
!!!!!!!!!!!!!!!

"determiner before gutturals"
a:á <=> %{h%}: _ %>: [ %': | %{%'%}: ] ;

!!!!!!!!!!!!!!!
!!! á
!!!!!!!!!!!!!!!

"feminine plural drop -áh: á"
á:0 <=> _ h: %>: w o t ;

!!!!!!!!!!!!!!!
!!! {~a}
!!!!!!!!!!!!!!!

"{~a} → á in lamed-hey"
%{%~a%}:á <=> _ ( Stress ) h ;

"{~a} → a in closed syllables"
%{%~a%}:a <=> _ ( Stress ) Cons VbNoSuf ;
              _ ( Stress ) Cons: %>: Cons ;
              except _ ( Stress ) [ h | Cons %>: w . ] ;

!!!!!!!!!!!!!!!
!!! {and}
!!!!!!!!!!!!!!!

"schwa in paal perf V1"
%{%~á%}:%@ <=> _ :+ %>: t . é %{%*%} [ m | n ] .#. ;
               _ ?* %>: ?* %>: ;

!!!!!!!!!!!!!!!
!!! {and}
!!!!!!!!!!!!!!!

"{and} before b"
%{and%}:%. <=> _ b: ;

"{and} before y"
%{and%}:a <=> _ ( %{%<%*%}: ) y ;

!!!!!!!!!!!!!!!
!!! @
!!!!!!!!!!!!!!!

"schwa deletes"
%@:0 <=> _ ( %>: ) %{h%}: ;
         %' _ ;
         [ l:0 | y:0 | n:0 ] _ Cons ( 0:. ) %{%~o%}: ;
         ImpfPref Cons _ w: ;

!!!!!!!!!!!!!!!
!!! e
!!!!!!!!!!!!!!!

"possessed plural construct 3sg"
e:á <=> %{cons%}: _ y: %{ps%}: w: ;

!!!!!!!!!!!!!!!
!!! é
!!!!!!!!!!!!!!!

"1sg impf by binyan"
é:Vx <=> %' _ %>: Bx: ;
         where Vx in ( ä        ä        a       á       )
               Bx in ( %{piel%} %{pual%} %{hif%} %{hof%} )
         matched ;

!!!!!!!!!!!!!!!
!!! {~e}
!!!!!!!!!!!!!!!

!"{~e} in closed non-final syllable"
!%{%~e%}:a <=> _ ( Stress ) Cons %>: Cons ;
!              execpt _ ( Stress ) Cons %>: n á ;

"{~e} deletes in open syllables"
%{%~e%}:%@ <=> _ ( Stress ) Cons %>: [ :Vowel | w . ] ;

"{~e} → e in closed syllables final syllables"
%{%~e%}:e <=> _ ( Stress ) Cons VbNoSuf ;
              [ %{piel%}: | %{hit%}: ] ?* _ ( Stress ) Cons: %>: n á ;
              except _ ( Stress ) [ h | Cons %>: w . ] ;

!!!!!!!!!!!!!!!
!!! h
!!!!!!!!!!!!!!!

"feminine plural drop -áh: h"
h:0 <=> á: _ %>: w o t ;

!!!!!!!!!!!!!!!
!!! {h}
!!!!!!!!!!!!!!!

"{h} → 0 after vowel"
%{h%}:0 <=> Vowel: ( %>: ) _ ;

!!!!!!!!!!!!!!!
!!! {i}
!!!!!!!!!!!!!!!

"paal/nifal {i}"
%{i%}:i <=> _ %>: [ %{paal%}: | %{nif%}: | %{hit%}: ] ;
            except _ ImpfBd [ PehGuttural | HollowVerb | y:0 | %{%~h%}:0 ] ;

"{i} before guttural or in hiph'il"
%{i%}:a <=> _ ImpfBd %{%'%} ;
            _ %>: %{hif%}: ;
            except _ %>: %{hif%}: y: ;

"i → á in impf hollow verbs"
%{i%}:á <=> _ ImpfBd HollowVerb ;
            _ %>: %{hof%}: ;
            except _ [ ImpfBd PehGuttural | %>: %{piel%}: | %>: %{hif%}: ] ;

"i → e in impf peh-yod"
%{i%}:e <=> _ ImpfBd [ y:0 | %{%~h%}:0 ] ;

"impf → @"
%{i%}:%@ <=> _ %>: [ %{piel%}: | %{pual%}: ] ;
             _ %>: %{paal%}: [ h | x ] ;
             except _ %>: Binyan: %{%'%} ;

"drop impf i"
%{i%}:w <=> _ %>: %{hif%}: y: ;

!!!!!!!!!!!!!!!
!!! {~iy}
!!!!!!!!!!!!!!!

"{~iy} → e when suffix is náh"
%{%~iy%}:e <=> _ y: ( Stress ) Cons %>: n á ;
               %{and%}: %{%<%*%}: ?* %{hif%}: y: ?* _ ;

"{~iy} → a in most closed syllables"
%{%~iy%}:a <=> _ y: ( Stress ) Cons %>: [ Cons - w ] ;
               except _ y: ( Stress ) Cons %>: n á ;

!!!!!!!!!!!!!!!
!!! l, n
!!!!!!!!!!!!!!!

"impf delete initial cons"
Cx:0 <=> %{i%}: %>: [ %{paal%}: | %{hif%}: ] _ ;
     where Cx in ( %{%~h%} l ) ;

"n deletes"
n:0 <=> %{i%}: %>: [ %{paal%}: | %{hif%}: ] _ ;
        _ %>: t ;

!!!!!!!!!!!!!!!
!!! {~o}
!!!!!!!!!!!!!!!

"{~o} deletes if suffix"
%{%~o%}:%@ <=> _ :* [ :Vowel - á ] ;
               _ :* w :. ;
               except C2Hollow _ ;

"hollow verbs wo"
%{%~o%}:. <=> w: _ ;

"hollow verbs o"
%{%~o%}:0 <=> %{%~w%}:o _ ;

"{~o} in peh-yod"
%{%~o%}:e <=> V2DelC1 _ ( Stress ) Cons VbNoSuf ;
              except C2Hollow _ ;

"{~o} special lamed"
%{%~o%}:Vx <=> _ ( Stress ) Cx VbNoSuf ;
               except [ C2Hollow | V2DelC1 ] _ ;
               where Vx in ( a á  é )
                     Cx in ( x %' h )
               matched ;

!!!!!!!!!!!!!!!
!!! y
!!!!!!!!!!!!!!!

"impf delete initial y"
y:0 <=> %{i%}: %>: [ Binyan: - %{hif%}: ] _ ;
        a y %>: i _ ;
        %{%~iy%}:e _ ;
        %{%~iy%}:a _ ;

"y in impf hifil"
y:o <=> %{i%}: %>: %{hif%}: _ ;

!!!!!!!!!!!!!!!
!!! OTHER
!!!!!!!!!!!!!!!

"w-o hollow verbs"
%{%~w%}:o <=> _ %{%~o%}: ;

"definite segolate nouns"
é:á <=> %{h%}: a: %>: %': _ ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

