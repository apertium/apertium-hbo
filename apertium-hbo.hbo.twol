Alphabet
%' b g d h w z x v y k l m n s %{'%} p c q r %{s%} %{sh%} t
a á e é i o u %@ ä ë ö
.

%>:0

! the lazy vowel
%{V%}:a %{V%}:á %{V%}:e %{V%}:é %{V%}:i %{V%}:o %{V%}:u
%{V%}:%@ %{V%}:ä %{V%}:ë %{V%}:ö

%{ps%}:0 %{ps%}:%@ %{ps%}:é %{ps%}:á %{ps%}:e

%{and%}:%. %{and%}:a %{and%}:%@

%{%~a%}:a %{%~a%}:%@
%{%~á%}:á %{%~á%}:%@
%{%~e%}:e %{%~e%}:%@
%{%~o%}:o %{%~o%}:%@ %{%~o%}:á %{%~o%}:é %{%~o%}:a

%{%~iy%}:i %{%~iy%}:0

%{h%}:h %{h%}:0

%{short%}:0
%{consec%}:0
%{cons%}:0
 ;

Sets

Begedkefet = b g d k p t ;
Vowel = a á e é i o u %@ ä ë ö ;

DageshAble = z y m c %{s%} %{sh%} ;

Rules

"insert dagesh"
0:. => .#. Begedkefet _ :Vowel ;
       %{consec%}: ? _ ;
       %{h%}: a: %>: DageshAble: _ ;
       i %>: [ l:0 | y:0 | n:0 ] %@:0 ? _ ;

"insert yod"
0:y => %{%~iy%}:i _ ;

"{h} → 0 after vowel"
%{h%}:0 <=> Vowel: ( %>: ) _ ;

"schwa deletes"
%@:0 <=> _ ( %>: ) %{h%}: ;
         %' _ ;
         i %>: [ l:0 | y:0 | n:0 ] _ ;

"peh-aleph verbs"
i:o <=> _ %>: %' ;

"lamed-he verbs"
h:0 <=> _ %>: %{short%}: ;

"lamed-he continued"
%{%~o%}:0 <=> _ h:0 %>: %{short%}: ;

"i → a in impf with lamed-he"
i:a <=> _ %>: ?* h:0 %>: %{short%}: ;
    except _ %>: [ %' | ?* y: ? h:0 %>: ] ;

"*yh in short"
i:%@ <=> _ %>: ?* y: ? h:0 %>: ;

"and the vowel"
%@:i <=> _ y ? h:0 %>: %{short%}: ;

"definite segolate nouns"
é:á <=> %{h%}: a: %>: %': _ ;

"blah"
a:á <=> %{h%}: _ %>: %': ;

"e:é cons"
e:é <=> _ ?* %{cons%}: .#. ;

"e:@ cons pl"
e:%@ <=> _ ?* %{cons%}: e: y: ;

"{and} before b"
%{and%}:%. <=> _ b: ;

"ay → ey cons"
a:e <=> _ y ?* %{cons%}: ;

"impf delete initial cons"
Cx:0 <=> i %>: _ ;
     where Cx in ( l y n ) ;

! Resources:
! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Enter_twol
! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
! https://github.com/hfst/
! http://wiki.apertium.org/wiki/Twol
! http://wiki.apertium.org/wiki/Hfst

